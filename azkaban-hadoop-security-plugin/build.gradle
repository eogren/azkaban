apply plugin: 'distribution'

configurations {
    provided
    compile.extendsFrom(provided)
}

dependencies {
    compile project(':az-core')
    compile project(":azkaban-common")
    provided deps.adal4J

    compileOnly deps.hadoopCommon
    compileOnly deps.hadoopMRClientCommon
    compileOnly deps.hadoopMRClientCore
    compileOnly deps.hiveMetastore
    compileOnly(deps.hiveExecCore) {
        exclude group: 'org.pentaho', module: 'pentaho-aggdesigner-algorithm'
        exclude group: 'eigenbase', module: 'eigenbase-properties'
    }

    testCompile deps.junit
}

sourceSets {
    test.compileClasspath += configurations.compileOnly
    test.runtimeClasspath += configurations.compileOnly
}

/**
 * TODO spyne: remove after fixing internal build.
 *
 * Just package the jar.
 * Since, rest of the dependencies are just hadoop and hive. They are not packaged inside the plugin.
 * It is assumed that classpaths of hadoop, hive, pig, etc will be externally fed into the application.
 */
distributions {
    main {
        contents {
            from(jar) {
                into 'lib'
            }
            from (configurations.provided) {
                into 'lib'
            }
        }
    }
}
